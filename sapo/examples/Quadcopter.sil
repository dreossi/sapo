problem: reachability;

iterations: 20;

var pn, pe in [0, 0];
var h in [0.20, 0.21];
var u, v, w in [0, 0];
var q0v in [1, 1];
var q1v, q2v, q3v, p, q, r, hI, uI, vI, psiI in [0, 0];

const M = 0.0015;
const mr = 0.001;
const R = 0.020;
const l = 0.045;
const g = 9.81;
const m = 0.0015 + 4*0.001;
const mInv = 181.81;
const Jx = (2*0.0015*0.02*0.02)*0.2 + 2*0.045*0.045*0.001;
const JxInv = 233100.23;
const Jy = (2*0.0015*0.02*0.02)*0.2 + 2*0.045*0.045*0.001;
const JyInv = 233100.23;
const Jz = (2*0.0015*0.02*0.02)*0.2 + 4*0.045*0.045*0.001;
const JzInv = 119904.07;

// Reference values
const ur = 0;
const vr = 0;
const psir = 0;
const hr = 1;

const delta = 0.01;

next(pn) = pn + (u*(2*q0v*q0v + 2*q1v*q1v - 1) - v*(2*q0v*q3v - 2*q1v*q2v ) + w*(2*q0v*q2v + 2*q1v*q3v ))*delta;
next(pe) = pe + (v*(2*q0v*q0v + 2*q2v*q2v - 1) + u*(2*q0v*q3v + 2*q1v*q2v ) - w*(2*q0v*q1v - 2*q2v*q3v ))*delta;
next(h) = h + (w*(2*q0v*q0v + 2*q3v*q3v - 1) - u*(2*q0v*q2v - 2*q1v*q3v ) + v*(2*q0v*q1v + 2*q2v*q3v ))*delta;

next(u) = u + (r*v - q*w - g*(2*q0v*q2v - 2*q1v*q3v ))*delta;
next(v) = v + (p*w - r*u + g*(2*q0v*q1v + 2*q2v*q3v ))*delta;
next(w) = w + (q*u - p*v -(0.0361*hI + 0.0694*h + 0.0603*w)*mInv + g*(2*q0v*q0v + 2*q3v*q3v - 1 ))*delta;

next(q0v) = q0v +(-(q1v*0.5)*p - (q2v*0.5)*q - (q3v*0.5)*r)*delta;
next(q1v) = q1v + ((q0v*0.5)*p - (q3v*0.5)*q + (q2v*0.5)*r)*delta;
next(q2v) = q2v + ((q3v*0.5)*p + (q0v*0.5)*q - (q1v*0.5)*r)*delta;
next(q3v) = q3v + ((q1v*0.5)*q - (q2v*0.5)*p + (q0v*0.5)*r)*delta;

next(p) = p + ((1*JxInv)*(-0.0003*vI - 0.0005*v - 0.0018*2*q1v - 0.0004*p) + ((Jy - Jz)*JxInv)*q*r)*delta;
next(q) = q + ((1*JyInv)*(0.0003*uI + 0.0005*u - 0.0018*2*q2v - 0.0004*q) - ((Jx - Jz)*JyInv)*p*r)*delta;
next(r) = r + ((1*JzInv)*(-0.0003*psiI - 0.0006*2*q3v - 0.0003*r) + ((Jx - Jy)*JzInv)*p*q)*delta;

// Controller
next(hI) = hI + (h - hr)*delta;
next(uI) = uI +(u - ur)*delta;
next(vI) = vI + (v - vr)*delta;
next(psiI) = psiI + (2*q3v - psir)*delta;


direction 0.5*h + 0.5*w + 0.5*q0v + 0.25*vI in [-100, 100];

template = {
	{0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16},
	{0, 1, 2, 3, 4, 17, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16}
};

option no_caching;
